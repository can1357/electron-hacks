# Maintainer: Can Bölük <me@can.ac>
pkgname=miro-electron
pkgver=1.4.4
pkgrel=1
pkgdesc="Miro desktop app for Linux (Electron wrapper with dark mode)"
arch=('x86_64')
url="https://github.com/can1357/electron-hacks"
license=('MIT')
depends=('electron' 'hicolor-icon-theme')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('SKIP')

package() {
    cd "$srcdir/electron-hacks-$pkgver"

    install -Dm644 miro/main.js "$pkgdir/usr/lib/$pkgname/main.js"
    install -Dm644 miro/dark.css "$pkgdir/usr/lib/$pkgname/dark.css"
    install -Dm644 miro/icon.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/miro.png"

    install -Dm755 /dev/stdin "$pkgdir/usr/bin/miro" <<EOF
#!/bin/bash
exec electron /usr/lib/$pkgname/main.js "\$@"
EOF

    install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/miro.desktop" <<EOF
[Desktop Entry]
Name=Miro
Comment=Online collaborative whiteboard
Exec=miro %U
Icon=miro
Type=Application
Categories=Office;ProjectManagement;
StartupWMClass=miro-electron
EOF

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE" 2>/dev/null || true
}
