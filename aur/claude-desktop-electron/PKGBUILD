# Maintainer: Can Bölük <me@can.ac>
pkgname=claude-desktop-electron
pkgver=1.2.0
pkgrel=1
pkgdesc="Claude AI desktop app for Linux (Electron wrapper)"
arch=('x86_64')
url="https://github.com/can1357/electron-hacks"
license=('MIT')
depends=('electron' 'hicolor-icon-theme')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('SKIP')

package() {
    cd "$srcdir/electron-hacks-$pkgver"

    install -Dm644 claude/main-desktop.js "$pkgdir/usr/lib/$pkgname/main.js"
    install -Dm644 claude/native-stub.js "$pkgdir/usr/lib/$pkgname/native-stub.js"
    install -Dm644 claude/icon.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/claude.png"
    cp -r claude/resources "$pkgdir/usr/lib/$pkgname/"

    install -Dm755 /dev/stdin "$pkgdir/usr/bin/claude-desktop" <<EOF
#!/bin/bash
exec electron /usr/lib/$pkgname/main.js "\$@"
EOF

    install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/claude-desktop.desktop" <<EOF
[Desktop Entry]
Name=Claude
Comment=Claude AI Assistant
Exec=claude-desktop %U
Icon=claude
Type=Application
Categories=Office;
StartupWMClass=Claude
EOF

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE" 2>/dev/null || true
}
